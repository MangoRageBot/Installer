buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:+'
    }
}

plugins {
    id 'org.gradlex.extra-java-module-info' version '1.12'
}


apply plugin: 'com.gradleup.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.mangorage'
version = getLatestGitTag() + "." + getLatestGitVersion() + "-beta"

println("Version: " + version)

extraJavaModuleInfo {
    setDeriveAutomaticModuleNamesFromFileNames(true)
}

def getLatestGitTag() {
    def result = "git describe --long --tags".execute().text.trim()
    if (result.empty) {
        throw new RuntimeException("Failed to retrieve commit count")
    } else {
        return result.split("-")[0]
    }
}


def getLatestGitVersion() {
    def result = "git describe --long --tags".execute().text.trim()
    if (result.empty) {
        throw new RuntimeException("Failed to retrieve commit count")
    } else {
        def commitCount = result.split("-")[1].toInteger()
        return commitCount
    }
}


repositories {
    mavenCentral()
}

java {
    modularity.inferModulePath = true

    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.google.code.gson:gson:2.10.1'

    shadow 'net.sf.jopt-simple:jopt-simple:5.0.4'
    shadow 'com.google.code.gson:gson:2.10.1'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.mangorage.installer.Installer',
                'ModuleMainClass': 'org.mangorage.installer.Installer'
        )
    }
}
task runInstaler(type: JavaExec) {
    group = 'Installer Tasks'
    description = 'Runs the main class in the build/run directory'

    mainClass.set('org.mangorage.installer.Installer') // replace with your actual main class
    classpath = sourceSets.main.runtimeClasspath

    workingDir = file("$buildDir/run")
    doFirst {
        workingDir.mkdirs() // Make sure the directory exists
    }
}

shadowJar {
    group "main tasks"
    archiveClassifier.set('')

    excludes.remove('module-info.class')
    relocate('com.google.gson', 'internal.shaded.com.google.gson')
    relocate('joptsimple', 'internal.shaded.joptsimple')
}

publishing {
    publications.register("installer", MavenPublication) {
        artifact shadowJar

        pom {
            name = 'MangoBot Installer'
            description = 'The Installer for my Discord Bot Project'
            url = 'https://github.com/MangoRageBot/Installer'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/Installer/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.minecraftforge.net/"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}